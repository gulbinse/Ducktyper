import com.diffplug.gradle.spotless.SpotlessExtension
import com.github.spotbugs.snom.Effort

plugins {
    // Apply the java Plugin to add support for Java.
    id 'java'
    id 'application'

    // Quality stuff
    id 'checkstyle'
    id "com.github.spotbugs" version "6.0.11"
    id "com.diffplug.spotless" version "6.25.0"

    id("org.openjfx.javafxplugin") version "0.1.0"
}

dependencies {
    implementation project(':communication')
}

application {
    // Define the main class for the application.
    mainClass = 'typeracer.client.Client'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'com.google.truth:truth:1.4.2'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

}

javafx {
    version = "21.0.3"
    modules(
            "javafx.base",
            "javafx.swing",
            "javafx.graphics",
            "javafx.controls",
            "javafx.fxml",
            "javafx.media",
            "javafx.web")
}

tasks.named("run") { standardInput = System.in }

tasks.withType(Javadoc) {
    options {
        addBooleanOption("Werror", true)
    }
}

tasks.named("test") {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

checkstyle {
    toolVersion = "10.15.0"
    maxWarnings = 0
}

spotbugs {
    effort = Effort.MAX
    ignoreFailures = false
}

tasks.spotbugsMain {
    reports.create("html") {
        required = true
        outputLocation = layout.buildDirectory.file("reports/spotbugs.html")
    }
}

tasks.spotbugsTest {
    reports.create("html") {
        required = true
        outputLocation = layout.buildDirectory.file("reports/spotbugsTests.html")
    }
}

spotless {
    java {
        googleJavaFormat()
        lineEndings 'UNIX'
    }
}